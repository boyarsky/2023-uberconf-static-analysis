plugins {
    id 'java'
    id 'checkstyle'
    id ('com.github.spotbugs') version '5.0.14'
    id 'pmd'
    id ('org.sonarqube') version '4.2.1.3168'
}

group = 'com.jeanneboyarsky.staticanalysis'
version = '1.0.0-SNAPSHOT'

repositories {
    flatDir {
        dirs 'generated-libs'
    }
    mavenCentral()
}

dependencies {
    // config based on https://github.com/takemikami/checkstyle-custom-checks
    checkstyle 'com.puppycrawl.tools:checkstyle:10.12.1'
    checkstyle files('generated-libs/custom-checkstyle-1.0.0-SNAPSHOT.jar')

    implementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    spotbugsPlugins 'com.jeanneboyarsky.spotbugs:custom-spotbugs:1.0.0-SNAPSHOT'

    // workaround until gradle supports PMD 7.x https://github.com/gradle/gradle/issues/24502
    pmd 'net.sourceforge.pmd:pmd-ant:7.0.0-rc3'
    pmd 'net.sourceforge.pmd:pmd-java:7.0.0-rc3'
    pmd files('generated-libs/custom-pmd-1.0.0-SNAPSHOT.jar')

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

checkstyle {
    // defaults to false
    // normally want that but showing multiples in this project
    ignoreFailures = true
}

spotbugs {
    toolVersion = '4.7.3'
    showProgress = true
    includeFilter = file('config/spotbugs/spotbugs-include.xml')

    // defaults to false
    // normally want that but showing multiples in this project
    ignoreFailures = true
}

// this approach has been deprecated since gradle 7
// there are several (duplicate) github issues to fix but hasn't been done yet
// still works in gradle 9
// https://github.com/spotbugs/spotbugs-gradle-plugin/issues/616
// https://github.com/spotbugs/spotbugs-gradle-plugin/issues/819
// https://github.com/spotbugs/spotbugs-gradle-plugin/issues/895
spotbugsMain {
    reports {
        // defaults to xml (can only generate html or xml per run)
        html {
            enabled = true
        }
    }
}

pmd {
    toolVersion = "7.0.0-rc3"

    // defaults to false
    // normally want that but showing multiples in this project
    ignoreFailures = true

    ruleSetFiles = files("config/pmd/pmd-ruleset.xml")
    // don't use default rulesets (seems to be optional now)
    ruleSets = []
}

sonar {
    properties {
        property 'sonar.projectKey', 'UberConf'
        property 'sonar.projectName', 'UberConf'
        property 'sonar.host.url', 'http://localhost:9000'
    }
}
